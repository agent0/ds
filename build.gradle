defaultTasks 'local'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version = '1.0-SNAPSHOT'
group = 'de.agentlab'

repositories {
    mavenCentral()
    flatDir(dirs: 'lib')
}

configurations {
    providedCompile
}

sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'ds-testsuite', parallel: 'tests') {
            test(name: 'common-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.common.*')
                }
            }
            test(name: 'graph-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.graph.*')
                }
            }
            test(name: 'table-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.table.*')
                }
            }
            test(name: 'tree-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.tree.*')
                }
            }
        }
    }

    ext.useDefaultListeners = true
    ext.workingDirectory = 'build/'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile(
            ['org.testng:testng:6.8.8'],
    )
}

jar {
    from('.') {
        include 'license.txt'
    }
    from('src/main/java') {
        include '**'
        into 'src'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'de.agentlab'
            artifactId = 'ds'
            version = '1.0-SNAPSHOT'

            from components.java

            pom {
                description = 'Common datastructures'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'jli'
                        name = 'JÃ¼rgen Lind'
                        email = 'jli@agentlab.de'
                    }
                }
                scm {
                    connection = 'https://github.com/agent0/ds.git'
                }
            }
        }
    }
    repositories {
        maven {
            def stagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
            credentials {
                username 'agent0'
                password ''
            }
        }
    }
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}

processTestResources.dependsOn copyTestResources
