defaultTasks 'local'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'de.agentlab'
archivesBaseName = 'ds'
version = '1.1'
//version = '1.2-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir(dirs: 'lib')
}

configurations {
    providedCompile
}

sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'ds-testsuite', parallel: 'tests') {
            test(name: 'common-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.common.*')
                }
            }
            test(name: 'graph-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.graph.*')
                }
            }
            test(name: 'table-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.table.*')
                }
            }
            test(name: 'tree-tests') {
                packages {
                    'package'(name: 'de.agentlab.ds.tree.*')
                }
            }
        }
    }

    ext.useDefaultListeners = true
    ext.workingDirectory = 'build/'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile(
            ['org.testng:testng:6.8.8'],
    )
}

jar {
    from('.') {
        include 'license.txt'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Common Datastructures'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Some common datastructures'
                url 'http://www.agentlab.de'

                scm {
                    connection = 'https://github.com/agent0/ds.git'
                    url 'https://github.com/agent0/ds'
                }

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'jli'
                        name = 'JÃ¼rgen Lind'
                        email = 'jli@agentlab.de'
                    }
                }
            }
        }
    }
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}

processTestResources.dependsOn copyTestResources
